options{
    STATIC = false;
    LOOKAHEAD = 2;
}

PARSER_BEGIN(Analizador)
public class Analizador{
    
    public static void main(String[] args){
        Analizador analizador = new Analizador(System.in);

        try{
            System.out.println("Iniciando análisis...\n");
            analizador.inicializarArbol();

            System.out.println("Análisis exitoso");
        }catch(ParseException e){
            System.out.println("Error de sintaxis: " + e.getMessage());
        }
    }

    class Nodo {
        Token token;
        Nodo izquierdo, derecho;

        public Nodo(Token token){
            this.token = token;
            this.izquierdo = null;
            this.derecho = null;
        }
    }

    class Arbol{
        Nodo raiz;

        public Arbol(){
            raiz = null;
        }

        public Arbol(Nodo raiz){
            this.raiz = raiz;
        }

        void enOrden(){
            System.out.print("\nRecorrido in-orden: ");
            enOrdenRec(raiz);
            System.out.println("\n");
        }

        void enOrdenRec(Nodo raiz){
            if (raiz != null) {
                enOrdenRec(raiz.izquierdo);
                System.out.print(raiz.token.image + " ");
                enOrdenRec(raiz.derecho);
            }
        }

        public void insertar(Arbol arbolIzq, Arbol arbolDer, Nodo operador){
            operador.izquierdo = arbolIzq.raiz;
            operador.derecho = arbolDer.raiz;
            this.raiz = operador;
        }

        public Nodo getRaiz(){
            return raiz;
        }

        public void setRaiz(Nodo raiz){
            this.raiz = raiz;
        }
    }

    class ConversionFNC{
        Arbol arbol;

        public ConversionFNC(Arbol arbol){
            this.arbol = arbol;
        }

        Nodo obtenerSubArbol(Nodo raiz){
            if (raiz == null) {
                return null;
            }

            Nodo nuevoNodo = new Nodo(new Token(raiz.token.kind,raiz.token.image));
            nuevoNodo.izquierdo = obtenerSubArbol(raiz.izquierdo);
            nuevoNodo.derecho = obtenerSubArbol(raiz.derecho);

            return nuevoNodo;
        }

        void generarFNC(){
            boolean detectaCambios = false;
            System.out.println("Generar FNC");
            do{
                detectaCambios = aplicarConversion(this.arbol.raiz);
            }while(detectaCambios);
    
            System.out.println();
        }

        boolean aplicarConversion(Nodo raiz){
            boolean detectaCambios = false;

            if (raiz != null) {
                aplicarConversion(raiz.izquierdo);
                detectaCambios = aplicarSustituciones(raiz);
                aplicarConversion(raiz.derecho);
            }
            return detectaCambios;
        }

        boolean aplicarSustituciones(Nodo raiz){
            boolean detectaCambios = false;
    
            if(raiz.token.kind == BICONDICIONAL){
                sustituyeBicondicional(raiz);
                detectaCambios = true;
            }else if(raiz.token.kind == CONDICIONAL){
                sustituyeCondicional(raiz);
                detectaCambios = true;
            }else{
                detectaCambios = false;
            }
            return detectaCambios;
        }

        void sustituyeBicondicional(Nodo raiz){
            Nodo nodoIzqA = obtenerSubArbol(raiz.izquierdo);
            Nodo nodoDerA = obtenerSubArbol(raiz.derecho);

            Nodo nodoIzqB = obtenerSubArbol(raiz.izquierdo);
            Nodo nodoDerB = obtenerSubArbol(raiz.derecho);

            raiz.token = new Token(CONJUNCION, "&");

            raiz.izquierdo.token = new Token(CONDICIONAL, ">");
            raiz.izquierdo.izquierdo = nodoIzqA;
            raiz.izquierdo.derecho = nodoDerA;

            raiz.derecho.token = new Token(CONDICIONAL, ">");
            raiz.derecho.izquierdo = nodoDerB;
            raiz.derecho.derecho = nodoIzqB;
        }

        void sustituyeCondicional(Nodo raiz){
            raiz.token = new Token(DISYUNCION, "|");

            Nodo nodoIzq = obtenerSubArbol(raiz.izquierdo);
            Nodo nodoDer = obtenerSubArbol(raiz.derecho);

            raiz.izquierdo.token = new Token(NEGACION, "¬");
            raiz.izquierdo.derecho = nodoIzq;

            raiz.derecho = nodoDer;
        }

        //Busca el nodo VARIABLE sin importar cuantas negaciones tenga antes y retorna un nuevo nodo con la misma información
        Nodo obtenerVariable(Nodo nodo){
            Nodo nodoAux;
            if(nodo.token.kind == NEGACION){
                nodoAux = new Nodo(new Token(NEGACION, "¬"));
                nodoAux.derecho = obtenerVariable(nodo.derecho);
                return nodoAux;
            }else{
                nodoAux = new Nodo(nodo.token);
                return nodoAux;
            }
        }

        //Comprueba si ambos hijos del nodo son variables para que se pueda aplicar la sustitución
        boolean aplicarSustitucion(Nodo nodo){
            if(nodo.izquierdo.token.kind == VARIABLE && nodo.derecho.token.kind == VARIABLE){
                return true;
            }else{
                return false;
            }
        }
    }
}

PARSER_END(Analizador)

TOKEN:{
    <BICONDICIONAL: "=">{System.out.println("BICONDICIONAL: " + image);}
    | <CONDICIONAL: ">">{System.out.println("CONDICIONAL: " + image);}
    | <DISYUNCION: "|">{System.out.println("DISYUNCION: " + image);}
    | <CONJUNCION: "&">{System.out.println("CONJUNCION: " + image);}
    | <NEGACION: "¬">{System.out.println("NEGACION: " + image);}
    | <VARIABLE: (["A"-"Z"])+>{System.out.println("VARIABLE: " + image);}
}

TOKEN:{
    <PARENTESIS_IZQ: "(">{System.out.println("PARENTESIS_IZQ: " + image);}
    | <PARENTESIS_DER: ")">{System.out.println("PARENTESIS_DER: " + image);}
    | <CORCHETE_IZQ: "[">{System.out.println("CORCHETE_IZQ: " + image);}
    | <CORCHETE_DER: "]">{System.out.println("CORCHETE_DER: " + image);}
    | <LLAVE_IZQ: "{">{System.out.println("LLAVE_IZQ: " + image);}
    | <LLAVE_DER: "}">{System.out.println("LLAVE_DER: " + image);}
}

SKIP:{
    " " | "\t" | "\n" | "\r" | "\r\n"
}

TOKEN:{
    <FORMULAVALIDA: "1">
    | <CONTRADICCION: "0">
}

void inicializarArbol():{
    Arbol arbol;
    ConversionFNC conversion;
}{
    arbol = condicionales(){System.out.println("Fin"); arbol.enOrden(); conversion = new ConversionFNC(arbol); conversion.generarFNC(); arbol.enOrden();}
}

Arbol condicionales():{
    Arbol arbolIzq, arbolDer;
    Token t;
}{
    arbolIzq = operacionesLogicas()( 
         <CONDICIONAL>   {t = token;} arbolDer = operacionesLogicas() { arbolIzq.insertar(arbolIzq, arbolDer, new Nodo(t)); }
        |<BICONDICIONAL> {t = token;} arbolDer = operacionesLogicas() { arbolIzq.insertar(arbolIzq, arbolDer, new Nodo(t)); })*
        {return arbolIzq;}
}

Arbol operacionesLogicas():{
    Arbol arbolIzq, arbolDer;
    Token t;
}{
    arbolIzq = procesarNegacion()(
         <CONJUNCION> {t = token;} arbolDer = procesarNegacion() { arbolIzq.insertar(arbolIzq, arbolDer, new Nodo(t)); }
        |<DISYUNCION> {t = token;} arbolDer = procesarNegacion() { arbolIzq.insertar(arbolIzq, arbolDer, new Nodo(t)); })*
        {return arbolIzq;}
}

Arbol procesarNegacion():{
    Arbol arbolIzq = new Arbol(); 
    Arbol arbolDer;
}{
    ((<NEGACION>{
        if(arbolIzq.raiz == null){
            arbolIzq.setRaiz(new Nodo(token));
        }else{
            Nodo actual = arbolIzq.raiz;
            while(actual.derecho != null){
                actual = actual.derecho;
            }
            actual.derecho = new Nodo(token);
        }
    } arbolDer = procesarNegacion())
    | arbolDer = procesarVariable()
    ){
        if(arbolIzq.raiz == null){
            arbolIzq.setRaiz(arbolDer.raiz);
        }
        else{
            Nodo actual = arbolIzq.raiz;
            while(actual.derecho != null){
                actual = actual.derecho;
            }
            actual.derecho = arbolDer.raiz;
        }
        return arbolIzq;
    }
}

Arbol procesarVariable():{
    Arbol arbol;
}{
    (<VARIABLE>{ arbol = new Arbol(new Nodo(token)); } | arbol = procesarAgrupaciones()){ return arbol; }
}

Arbol procesarAgrupaciones():{
    Arbol arbol;
}{
      <PARENTESIS_IZQ> arbol = condicionales() <PARENTESIS_DER>{ return arbol; }
    | <CORCHETE_IZQ>   arbol = condicionales() <CORCHETE_DER>{ return arbol; }
    | <LLAVE_IZQ>      arbol = condicionales() <LLAVE_DER>{ return arbol; }
}